#+ summary: Get all taxon-taxon relation nanopublications
#+ method: GET
#+ pagination: 1000
#+ endpoint_in_url: False

prefix np: <http://www.nanopub.org/nschema#>
prefix npa: <http://purl.org/nanopub/admin/>
prefix npx: <http://purl.org/nanopub/x/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix dct: <http://purl.org/dc/terms/>
prefix biolink: <https://w3id.org/biolink/vocab/>
prefix nt: <https://w3id.org/np/o/ntemplate/>
prefix biodiv: <https://w3id.org/kpxl/biodiv/terms/>
prefix prov: <http://www.w3.org/ns/prov#>

select distinct ?np ?label ?subjtaxonname ?subjtaxonlabel ?rel ?objtaxonname ?objtaxonlabel ?source ?date where {
  graph npa:graph {
    ?np npa:hasHeadGraph ?h .
    ?np dct:created ?date .
    ?np npa:hasValidSignatureForPublicKey ?pubkey .
  }
  graph ?h {
    ?np np:hasAssertion ?a .
    ?np np:hasProvenance ?p.
    ?np np:hasPublicationInfo ?i.
  }
  graph ?a {
    ?association a biolink:OrganismTaxonToOrganismTaxonAssociation .
    ?association biolink:subject ?subjtaxon .
    ?subjtaxon biodiv:hasTaxonName ?subjtaxonname .
    ?association biolink:predicate ?rel .
    ?association biolink:object ?objtaxon .
    ?objtaxon biodiv:hasTaxonName ?objtaxonname .
  }
  graph <http://purl.org/np/RAodaWZBY-yDEtl9reYazBfI-YVD5L4zPh8RrVFS0kbEo#assertion> {
    ?rel rdfs:label ?rellabel .
  }
  optional {
    graph ?p {
      ?a prov:wasDerivedFrom ?source
    }
  }
  graph ?i {
    ?np dct:creator ?__creator_iri .
    ?np rdfs:label ?labelx .
    bind(str(?labelx) as ?label)
    optional { ?subjtaxonname nt:hasLabelFromApi ?subjtaxonlabel . }
    optional { ?objtaxonname nt:hasLabelFromApi ?objtaxonlabel . }
  }
  filter not exists {
    graph npa:graph {
      ?newversion npa:hasHeadGraph ?nh .
      ?newversion npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?nh {
      ?newversion np:hasPublicationInfo ?ni .
    }
    graph ?ni {
      ?newversion npx:supersedes ?np .
    }
  }
  filter not exists {
    graph npa:graph {
      ?retraction npa:hasHeadGraph ?rh .
      ?retraction npa:hasValidSignatureForPublicKey ?pubkey .
    }
    graph ?rh {
      ?retraction np:hasAssertion ?ra .
    }
    graph ?ra {
      ?somebody npx:retracts ?np .
    }
  }
}
order by desc(?date)
